{new GHPanel
   main_panel
   (file main.milo)
   (focus main_quickspin.btn)
   (msg_last_frame 0)
   (already_entered FALSE)
   #ifdef HX_EE
   (memcard_flow_entered FALSE)
   #endif
   (retain_last_focus FALSE)
   (enter
      {set $main_entered TRUE}
      {set $leaderboards_mode FALSE}
      #ifdef HX_XBOX
      #ifdef GAME_GH2
      {$this disable main_leaderboards.btn}
      #endif
      #endif
      {practice_sel_section_screen set career_mode FALSE}
      {$this reset_player_settings}
      {if {! [already_entered]}
         {synth play_sequence meta_lick}
         {if {&& {== {game get_controller 0} guitar} {== {campaign tutorials_done} 0}}
            {$this set_focus main_tutorial.btn}
         }
         {$this set already_entered TRUE}
      }
      {game set_tutorial_running FALSE}
      {main_msg.view set_showing FALSE}
      {set [msg_last_frame] 0}
      #ifdef HX_XBOX
      {gamecfg set_player 0 {profilemgr active_padnum}}
      #endif
      {gamecfg set mode quickplay}
      ;permadeath qol, sets first button to career
      {if {! $set_to_career_once}
         {set $set_to_career_once TRUE}
         {$this set_focus main_career.btn}
      }
      ;permadeath disables quickplay
      {if_else $permadeath
         {$this disable main_quickspin.btn}
         {$this enable main_quickspin.btn}
      }
      ;don't scan dlc
      #ifdef HX_XBOX
         #ifdef GAME_GH2
         {if $scandlc
            {content_mgr start_enumeration}
         }
         #endif
      #endif
      ;enable hyperspeed
      {if {&& {! $cheat_enabled_hyper_speed_mode} {! $turned_off}}
         {set $cheat_enabled_hyper_speed_mode TRUE}
         {main_screen display_cheat_msg cheat_hyper_speed_mode TRUE}
         {play_sfx cheat}
      }
   )
   (skip_career_setup
      {if {&& $permadeath $set_first_name_once}
         {campaign set_profile_name $first_name 0}
      }
      {if $set_first_char_once
         {game set_character $first_char TRUE}
      }
      {if $set_first_guitar_once
         {game set_guitar
            $first_guitar_body
            $first_guitar_skin
         }
      }
      {if $set_first_diff_once
         {{game get_player_config 0} set_difficulty $first_diff}
      }
      {do
         ($new_campaign {campaign new_campaign})
         {campaign set_character_info}
         {if_else $new_campaign
            {ui goto_screen tour_start_screen}
            {ui goto_screen career_screen}
         }
      }
   )
   (SELECT_START_MSG
      {switch $component
         (main_career.btn
            {gamecfg set mode career}
            {if_else {&& $permadeath $set_first_name_once $set_first_char_once $set_first_guitar_once $set_first_diff_once}
               {$this skip_career_setup}
               {if_else {> {campaign num_profiles} 0}
                  {ui goto_screen chooseprof_screen}
                  {do
                     {nameprof_screen set back_screen main_screen}
                     {nameprof_screen set is_editing FALSE}
                     {if_else $permadeath
                        {nameprof_screen set profile_slot 0}
                        {if_else $randomizer
                           {nameprof_screen set profile_slot 1}
                           {if_else $gym_unlock_all
                              {nameprof_screen set profile_slot 3}
                              {nameprof_screen set profile_slot 2}
                           }
                        }
                     }
                     {nameprof_screen set next_screen sel_difficulty_screen}
                     {ui goto_screen nameprof_screen}
                  }
               }
            }
         )
         (main_quickspin.btn
            {gamecfg set mode quickplay}
            {ui goto_screen qp_selsong_screen}
         )
         (main_multiplayer.btn
            #ifdef HX_XBOX
            {ui goto_screen multi_controller_select_screen}
            #else
            {ui goto_screen multi_screen}
            #endif
         )
         (main_tutorial.btn {ui goto_screen training_screen})
         (main_leaderboards.btn
            {if_else {profilemgr is_active_pad_signed_in_live}
               {do
                  {gamecfg set mode quickplay}
                  {leaderboards_song_screen set list_index 0}
                  {set $leaderboards_mode TRUE}
                  {ui goto_screen leaderboards_song_screen}
               }
               {do
                  {helpbar set_display ((fret1 help_continue))}
                  {ui push_screen main_not_logged_in_screen}
               }
            }
         )
         (main_options.btn {ui goto_screen options_screen})
      }
   )
   (poll
      #ifdef HX_EE
      {if_else {game is_missing_multi_controller}
         {$this disable main_multiplayer.btn}
         {$this enable main_multiplayer.btn}
      }
      #else
      {if_else {game is_multiple_controllers}
         {$this enable main_multiplayer.btn}
         {$this disable main_multiplayer.btn}
      }
      #endif
      {main_msg.view set_showing
         {< {taskmgr ui_seconds} {$this get msg_last_frame}}
      }
   )
   (reset_player_settings
      {{game get_player_config 0} set_difficulty kDifficultyExpert}
      #ifdef HX_XBOX
      {song_provider set_mode kSongsCareer}
      {song_provider init_data}
      #else
      {game set_venue small2}
      #endif
      {game set_song_index 0}
      {game set_character punk1 TRUE}
      {game set_guitar_index 0}
      {do
         ($player2_config {game get_player_config 1})
         {$player2_config set_character
            #ifdef HX_XBOX
            metal1
            #else
            goth2
            #endif
            TRUE
         }
         {$player2_config set_guitar_index 0}
         {$player2_config set_difficulty kDifficultyExpert}
      }
   )
   (display_cheat_msg
      ($cheat $enable)
      {mm_msg.lbl set_localized_text
         {sprintf
            {localize
               {if_else $enable
                  cheat_enabled_fmt
                  cheat_disabled_fmt
               }
            }
            {localize $cheat}
         }
      }
      {$this set msg_last_frame {'+' {taskmgr ui_seconds} 5}}
   )
}
{new GHScreen
   main_screen
   (panels meta main_panel helpbar)
   (focus main_panel)
   (helpbar
      (default
         (
            (fret1 help_continue)
            (strum help_updown)
         )
      )
   )
   (access_all {main_panel reset_player_settings})
   (enter {$this turn_off_ambient})
}
#ifdef HX_XBOX
{new GHPanel
   main_not_logged_in_panel
   (file not_signed_in.milo)
   (focus continue.btn)
}
{new GHScreen
   main_not_logged_in_screen
   (panels main_not_logged_in_panel)
   (focus main_not_logged_in_panel)
   (allow_back FALSE)
   (animate_transition FALSE)
   (SELECT_START_MSG
      {switch $component
         (continue.btn {ui pop_screen})
      }
   )
}
#endif